plugins {
  id 'application'
  id 'org.graalvm.buildtools.native'
}

dependencies {
  implementation project(':configuration')
  implementation project(':logging-api')
  implementation 'com.beust:jcommander:1.82'
  if (project.properties.getOrDefault('appLogger', 'stdout') == 'slf4j') {
    implementation project(':logging-slf4j')
  }
}

application {
  mainClass = 'org.abelsromero.demo.App'
}

tasks.named('test') {
  useJUnitPlatform()
}

graalvmNative {
  binaries {
    main {
      mainClass = application.mainClass
    }
  }
  if (project.properties.get('gc')) {
    binaries.all {
      buildArgs.addAll("--gc=${project.properties.get('gc')}")
    }
  }
}

jar {
  manifest {
    attributes 'Main-Class': application.mainClass
  }
}

