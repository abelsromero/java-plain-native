plugins {
  id 'application'
  id 'org.graalvm.buildtools.native'
}

dependencies {
  implementation project(':java-modules:cli-args-api')
  implementation project(':java-modules:cli-args-jcommander')
  implementation project(':java-modules:configuration')
  implementation project(':java-modules:logging-api')
  implementation project(':java-modules:output-formatting')
  if (project.properties.getOrDefault('appLogger', 'stdout') == 'slf4j') {
    implementation project(':java-modules:logging-slf4j')
  }
}

application {
  mainClass = 'org.abelsromero.demo.App'
}

tasks.named('test') {
  useJUnitPlatform()
}

graalvmNative {
  binaries {
    main {
      imageName = 'cli'
      mainClass = application.mainClass
    }
  }
  if (project.properties.get('gc')) {
    binaries.all {
      buildArgs.addAll("--gc=${project.properties.get('gc')}")
    }
  }
}

jar {
  manifest {
    attributes 'Main-Class': application.mainClass
  }
}

